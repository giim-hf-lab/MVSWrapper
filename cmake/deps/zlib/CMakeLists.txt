FetchContent_Declare(zlib
	GIT_REPOSITORY "$CACHE{__C_GITHUB_PREFIX}madler/zlib.git"
	GIT_TAG "v${ZLIB_VERSION}"
	GIT_SUBMODULES_RECURSE TRUE
	GIT_SHALLOW "${__G_FETCHCONTENT_GIT_SHALLOW}"
	GIT_REMOTE_UPDATE_STRATEGY CHECKOUT
	OVERRIDE_FIND_PACKAGE
)

find_package(zlib REQUIRED GLOBAL)

FetchContent_GetProperties(zlib SOURCE_DIR __L_ZLIB_DIR BINARY_DIR __L_ZLIB_BINARY_DIR)
file(CREATE_LINK "${__L_ZLIB_BINARY_DIR}/zconf.h" "${__L_ZLIB_DIR}/zconf.h" SYMBOLIC)

set_property(
	TARGET
		zlib
		zlibstatic
	PROPERTY SYSTEM TRUE
)

get_archive_path("zlibstatic" __L_ZLIB_LIBRARY)

set(ZLIB_FOUND TRUE PARENT_SCOPE)
set(ZLIB_INCLUDE_DIRS "${__L_ZLIB_DIR}" PARENT_SCOPE)
set(ZLIB_LIBRARIES "${__L_ZLIB_LIBRARY}" PARENT_SCOPE)
