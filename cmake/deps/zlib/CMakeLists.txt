FetchContent_Declare(ZLIB
	GIT_REPOSITORY "$CACHE{__C_GITHUB_PREFIX}madler/zlib.git"
	GIT_TAG "v$CACHE{__C_ZLIB_VERSION}"
	GIT_SUBMODULES_RECURSE TRUE
	GIT_SHALLOW "${__G_FETCHCONTENT_GIT_SHALLOW}"
	GIT_REMOTE_UPDATE_STRATEGY CHECKOUT
	OVERRIDE_FIND_PACKAGE
)

find_package(ZLIB "$CACHE{__C_ZLIB_VERSION}" REQUIRED GLOBAL)

FetchContent_GetProperties(zlib SOURCE_DIR __L_ZLIB_DIR BINARY_DIR __L_ZLIB_BINARY_DIR)
file(CREATE_LINK "${__L_ZLIB_BINARY_DIR}/zconf.h" "${__L_ZLIB_DIR}/zconf.h" SYMBOLIC)

set_property(
	TARGET
		zlib
		zlibstatic
	PROPERTY SYSTEM TRUE
)

get_archive_path("zlibstatic" __L_ZLIB_LIBRARY)

set(ZLIB_FOUND TRUE PARENT_SCOPE)
set(ZLIB_INCLUDE_DIRS "${__L_ZLIB_DIR}" PARENT_SCOPE)
set(ZLIB_LIBRARIES "${__L_ZLIB_LIBRARY}" PARENT_SCOPE)
set(ZLIB_VERSION "$CACHE{__C_ZLIB_VERSION}" PARENT_SCOPE)
set(ZLIB_VERSION_STRING "$CACHE{__C_ZLIB_VERSION}" PARENT_SCOPE)

string(REPLACE "." ";" __L_ZLIB_VERSION_LIST "$CACHE{__C_ZLIB_VERSION}")
list(GET __L_ZLIB_VERSION_LIST 0 __L_ZLIB_VERSION_MAJOR)
list(GET __L_ZLIB_VERSION_LIST 1 __L_ZLIB_VERSION_MINOR)
list(GET __L_ZLIB_VERSION_LIST 2 __L_ZLIB_VERSION_PATCH)

set(ZLIB_VERSION_MAJOR "${__L_ZLIB_VERSION_MAJOR}" PARENT_SCOPE)
set(ZLIB_VERSION_MINOR "${__L_ZLIB_VERSION_MINOR}" PARENT_SCOPE)
set(ZLIB_VERSION_PATCH "${__L_ZLIB_VERSION_PATCH}" PARENT_SCOPE)
