string(REPLACE "." "-" __L_FREETYPE_VERSION_STRING "${FREETYPE_VERSION}")

set(FT_DISABLE_ZLIB OFF)
set(FT_REQUIRE_ZLIB ON)
set(FT_DISABLE_PNG OFF)
set(FT_REQUIRE_PNG ON)
set(FT_DISABLE_HARFBUZZ OFF)
set(FT_ENABLE_ERROR_STRINGS ON)

FetchContent_Declare(freetype
	GIT_REPOSITORY "$CACHE{__C_GITHUB_PREFIX}freetype/freetype.git"
	GIT_TAG "VER-${__L_FREETYPE_VERSION_STRING}"
	GIT_SUBMODULES_RECURSE TRUE
	GIT_SHALLOW "${__G_FETCHCONTENT_GIT_SHALLOW}"
	GIT_REMOTE_UPDATE_STRATEGY CHECKOUT
	OVERRIDE_FIND_PACKAGE
)

find_package(freetype REQUIRED GLOBAL)

set_property(
	TARGET
		freetype
	PROPERTY SYSTEM TRUE
)

get_archive_path("freetype" __L_FREETYPE_LIBRARY)

set(FREETYPE_FOUND TRUE)
get_property(FREETYPE_INCLUDE_DIRS
	TARGET freetype
	PROPERTY INTERFACE_INCLUDE_DIRECTORIES
)
set(FREETYPE_LIBRARIES "${__L_FREETYPE_LIBRARY}")
