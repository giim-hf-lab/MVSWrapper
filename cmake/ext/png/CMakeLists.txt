prefixed_option(PNG_SHARED "BUILD_SHARED_LIBS" "Build shared mimalloc."
	REFERENCE
)

get_ext_version(png __L_VERSION)

set(PNG_BUILD_ZLIB OFF)
get_prefixed_option(PNG_SHARED)
set(PNG_STATIC ON)
set(PNG_HARDWARE_OPTIMIZATIONS ON)

FetchContent_Declare(png
	GIT_REPOSITORY "${__G_GITHUB_PREFIX}glennrp/libpng.git"
	GIT_TAG "v${__L_VERSION}"
	GIT_SUBMODULES_RECURSE TRUE
	GIT_SHALLOW "${__G_FETCHCONTENT_GIT_SHALLOW}"
	GIT_REMOTE_UPDATE_STRATEGY CHECKOUT
	OVERRIDE_FIND_PACKAGE
)

FetchContent_MakeAvailable(png)
FetchContent_GetProperties(png SOURCE_DIR __L_DIR BINARY_DIR __L_BINARY_DIR)
file(COPY_FILE "${__L_BINARY_DIR}/pnglibconf.h" "${__L_DIR}/pnglibconf.h" ONLY_IF_DIFFERENT)

set(__L_TARGETS "png_static")
if(PNG_SHARED)
	list(APPEND __L_TARGETS "png")
endif()

get_archive_path("png_static" "${__L_BINARY_DIR}" __L_LIBRARY)

string(REPLACE "." ";" __L_VERSION_LIST "${__L_VERSION}")
list(GET __L_VERSION_LIST 0 __L_VERSION_MAJOR)
list(GET __L_VERSION_LIST 1 __L_VERSION_MINOR)
list(GET __L_VERSION_LIST 2 __L_VERSION_PATCH)

set(PNG_FOUND TRUE PARENT_SCOPE)
set(PNG_VERSION "${__L_VERSION}" PARENT_SCOPE)
set(PNG_VERSION_STRING "${__L_VERSION}" PARENT_SCOPE)
set(PNG_VERSION_MAJOR "${__L_VERSION_MAJOR}" PARENT_SCOPE)
set(PNG_VERSION_MINOR "${__L_VERSION_MINOR}" PARENT_SCOPE)
set(PNG_VERSION_PATCH "${__L_VERSION_PATCH}" PARENT_SCOPE)

set(PNG_DEFINITIONS "" PARENT_SCOPE)
set(PNG_INCLUDE_DIRS "${__L_DIR}" PARENT_SCOPE)
set(PNG_LIBRARIES "${__L_LIBRARY}" PARENT_SCOPE)

# only for OpenCV
set(PNG_PNG_INCLUDE_DIR "${__L_DIR}" PARENT_SCOPE)
set(PNG_INCLUDE_DIR "${__L_DIR}" PARENT_SCOPE)
set(PNG_LIBRARY "${__L_LIBRARY}" PARENT_SCOPE)
