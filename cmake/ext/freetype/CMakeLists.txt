string(REPLACE "." "-" __L_FREETYPE_VERSION_STRING "$CACHE{__C_FREETYPE_VERSION}")

set(FT_REQUIRE_ZLIB ON)
set(FT_REQUIRE_PNG ON)
set(FT_DISABLE_HARFBUZZ OFF)
set(FT_ENABLE_ERROR_STRINGS ON)

FetchContent_Declare(Freetype
	GIT_REPOSITORY "$CACHE{__C_GITHUB_PREFIX}freetype/freetype.git"
	GIT_TAG "VER-${__L_FREETYPE_VERSION_STRING}"
	GIT_SUBMODULES_RECURSE TRUE
	GIT_SHALLOW "${__G_FETCHCONTENT_GIT_SHALLOW}"
	GIT_REMOTE_UPDATE_STRATEGY CHECKOUT
	OVERRIDE_FIND_PACKAGE
)

find_package(Freetype "$CACHE{__C_FREETYPE_VERSION}" REQUIRED GLOBAL)

set_property(
	TARGET
		freetype
	PROPERTY SYSTEM TRUE
)

get_archive_path("freetype" __L_FREETYPE_LIBRARY)
get_property(__L_FREETYPE_INCLUDE_DIRS
	TARGET freetype
	PROPERTY INTERFACE_INCLUDE_DIRECTORIES
)

set(FREETYPE_FOUND TRUE PARENT_SCOPE)
set(FREETYPE_INCLUDE_DIRS "${__L_FREETYPE_INCLUDE_DIRS}" PARENT_SCOPE)
set(FREETYPE_LIBRARIES "${__L_FREETYPE_LIBRARY}" PARENT_SCOPE)
set(FREETYPE_VERSION "$CACHE{__C_FREETYPE_VERSION}" PARENT_SCOPE)
set(FREETYPE_VERSION_STRING "$CACHE{__C_FREETYPE_VERSION}" PARENT_SCOPE)
