set(FT_REQUIRE_ZLIB ON)
set(FT_REQUIRE_PNG ON)
set(FT_DISABLE_HARFBUZZ ON)
set(FT_ENABLE_ERROR_STRINGS ON)

get_ext_version(freetype __L_VERSION)
string(REPLACE "." "-" __L_VERSION_STRING "${__L_VERSION}")

FetchContent_Declare(freetype
	GIT_REPOSITORY "${__G_GITHUB_PREFIX}freetype/freetype.git"
	GIT_TAG "VER-${__L_VERSION_STRING}"
	GIT_SUBMODULES_RECURSE TRUE
	GIT_SHALLOW "${__G_FETCHCONTENT_GIT_SHALLOW}"
	GIT_REMOTE_UPDATE_STRATEGY CHECKOUT
	OVERRIDE_FIND_PACKAGE
)

FetchContent_MakeAvailable(freetype)
FetchContent_GetProperties(freetype BINARY_DIR __L_BINARY_DIR)

get_archive_path("freetype" "${__L_BINARY_DIR}" __L_LIBRARY)

string(REPLACE "." ";" __L_VERSION_LIST "${__L_VERSION}")
list(GET __L_VERSION_LIST 0 __L_VERSION_MAJOR)
list(GET __L_VERSION_LIST 1 __L_VERSION_MINOR)
list(GET __L_VERSION_LIST 2 __L_VERSION_PATCH)

get_property(__L_INCLUDE_DIRS
	TARGET freetype
	PROPERTY INTERFACE_INCLUDE_DIRECTORIES
)

set(FREETYPE_FOUND TRUE PARENT_SCOPE)
set(FREETYPE_VERSION "${__L_VERSION}" PARENT_SCOPE)
set(FREETYPE_VERSION_STRING "${__L_VERSION}" PARENT_SCOPE)
set(FREETYPE_VERSION_MAJOR "${__L_VERSION_MAJOR}" PARENT_SCOPE)
set(FREETYPE_VERSION_MINOR "${__L_VERSION_MINOR}" PARENT_SCOPE)
set(FREETYPE_VERSION_PATCH "${__L_VERSION_PATCH}" PARENT_SCOPE)

set(FREETYPE_INCLUDE_DIRS "${__L_INCLUDE_DIRS}" PARENT_SCOPE)
set(FREETYPE_LIBRARIES "${__L_LIBRARY}" PARENT_SCOPE)
