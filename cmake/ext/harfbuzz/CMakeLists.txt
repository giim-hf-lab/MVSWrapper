set(HB_HAVE_FREETYPE ON)

if (WIN32)
	set(HB_HAVE_UNISCRIBE ON)
	set(HB_HAVE_GDI ON)
	set(HB_HAVE_DIRECTWRITE ON)
endif ()

FetchContent_Declare(harfbuzz
	GIT_REPOSITORY "$CACHE{__C_GITHUB_PREFIX}harfbuzz/harfbuzz.git"
	GIT_TAG "$CACHE{__C_HARFBUZZ_VERSION}"
	GIT_SUBMODULES_RECURSE TRUE
	GIT_SHALLOW "${__G_FETCHCONTENT_GIT_SHALLOW}"
	GIT_REMOTE_UPDATE_STRATEGY CHECKOUT
	OVERRIDE_FIND_PACKAGE
)

find_package(harfbuzz "$CACHE{__C_HARFBUZZ_VERSION}" REQUIRED GLOBAL)

set_property(
	TARGET
		harfbuzz
	PROPERTY SYSTEM TRUE
)

get_archive_path("harfbuzz" __L_HARFBUZZ_LIBRARY)

get_property(__L_HARFBUZZ_INCLUDE_DIRS
	TARGET harfbuzz
	PROPERTY INTERFACE_INCLUDE_DIRECTORIES
)
set(__L_HARFBUZZ_LIBRARIES "${__L_HARFBUZZ_LIBRARY}" )
if (WIN32)
	list(APPEND __L_HARFBUZZ_LIBRARIES
		"gdi32"
		"usp10"
		"rpcrt4"
	)
endif ()

set(HARFBUZZ_FOUND TRUE PARENT_SCOPE)
set(HARFBUZZ_INCLUDE_DIRS "${__L_HARFBUZZ_INCLUDE_DIRS}" PARENT_SCOPE)
set(HARFBUZZ_LIBRARIES ${__L_HARFBUZZ_LIBRARIES} PARENT_SCOPE)
set(HARFBUZZ_VERSION "$CACHE{__C_HARFBUZZ_VERSION}" PARENT_SCOPE)
set(HARFBUZZ_VERSION_STRING "$CACHE{__C_HARFBUZZ_VERSION}" PARENT_SCOPE)
